import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

buildscript {
    ext {
        kotlinVersion = "1.2.0"
        springBootVersion = "1.5.8.RELEASE"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://repo.spring.io/libs-milestone" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.0.0.M6"
        classpath "com.bmuschko:gradle-docker-plugin:3.2.1"
    }
}

plugins {
    id "com.moowork.node" version "1.2.0"
}

apply plugin: "java"
apply plugin: "maven"
apply plugin: "idea"
apply plugin: "kotlin"
apply plugin: "kotlin-spring"
apply plugin: "org.springframework.boot"
apply plugin: "com.bmuschko.docker-remote-api"

group "io.zhudy.duic"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/libs-milestone" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.1"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.1"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:2.9.1"

    compile "org.springframework.boot:spring-boot-starter:2.0.0.M6"
    compile "io.javalin:javalin:1.1.0"
    compile "com.google.guava:guava:23.5-jre"
    compile "org.springframework.data:spring-data-mongodb:2.0.1.RELEASE"
    compile "redis.clients:jedis:2.9.0"
    compile "joda-time:joda-time:2.9.9"
    compile "org.hashids:hashids:1.0.3"
    compile "com.auth0:java-jwt:3.3.0"
    compile "org.springframework.security:spring-security-crypto:5.0.0.RELEASE"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

node {
    version = "7.7.1"
    npmVersion = "4.1.2"
    download = true
}

//task cnpmInstall(type: NpmTask) {
//    group = "node"
//    args = ["install", "--registry=http://registry.cnpmjs.org"]
//}

task buildAdmin(type: NpmTask) {
    group = "node"
    args = ["run", "build"]
}

task copyAdminFiles(type: Copy) {
    group = "node"
    from "dist"
    into "${project.buildDir}/resources/main"
}
jar.dependsOn copyAdminFiles

docker {
    registryCredentials {
        url = "https://index.docker.io/v1/"
        username = "kevin70"
        password = "njSzbjie0+"
        email = "kevinz@weghst.com"
    }
}

task createDockerfile(type: Dockerfile, dependsOn: jar) {
    group = "docker"

    copy {
        from jar.archivePath.path
        into "${project.buildDir}/docker"
        rename { String fileName ->
            fileName.replace("-${project.version}", "")
        }
    }

    def jarName = "${project.name}.jar".toString()

    destFile = project.file("${project.buildDir}/docker/Dockerfile")

    from "openjdk:8-jdk-alpine"
    maintainer "Kevin Zou <kevinz@weghst.com>"
    copyFile(jarName, jarName)
    environmentVariable("JAVA_OPTS", "-server -XX:+HeapDumpOnOutOfMemoryError -XX:+PrintGCDetails -Xloggc:logs/gc.log")
    exposePort(7777)
    entryPoint("java", "-jar", jarName, "\$JAVA_OPTS")
}

task buildImage(type: DockerBuildImage, dependsOn: createDockerfile) {
    group = "docker"
    inputDir = createDockerfile.destFile.parentFile
    tags = ["zhudyos/${project.name}:${project.version}".toString(), "zhudyos/${project.name}:latest".toString()]
}
